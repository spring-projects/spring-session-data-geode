plugins {
	id "io.freefair.lombok" version "5.0.0-rc6"
}

apply plugin: 'io.spring.convention.spring-module'
apply from: MAVEN_POM_EDITOR_GRADLE

description = "Spring Session for Apache Geode"

dependencies {

	compile "org.springframework:spring-context-support"
	compile "org.springframework:spring-jcl"
	compile "org.springframework.data:spring-data-geode"
	compile "org.springframework.session:spring-session-core:$springSessionVersion"

	optional "org.springframework.security:spring-security-core"
	optional "org.springframework.security:spring-security-web"

	provided "javax.servlet:javax.servlet-api"

	// TODO: Remove when all internal Apache Geode API usage is removed; Only required by tests!
	provided "org.apache.geode:geode-logging:$apacheGeodeVersion"
	provided "org.apache.geode:geode-serialization:$apacheGeodeVersion"

	testCompile "org.assertj:assertj-core"
	testCompile "junit:junit"
	testCompile "org.mockito:mockito-core"
	testCompile "edu.umd.cs.mtc:multithreadedtc"
	testCompile "org.springframework:spring-test"
	testCompile "org.springframework:spring-web"
	testCompile "org.springframework.data:spring-data-geode-test"
	testCompile slf4jDependencies

	testRuntime "ch.qos.logback:logback-classic"
	testRuntime "org.apache.logging.log4j:log4j-to-slf4j"

	integrationTestRuntime "org.springframework.shell:spring-shell"

}

test {

	maxHeapSize = project.hasProperty("test.jvm.heap.max-size")
		? project.getProperty("test.jvm.heap.max-size")
		: "512m"

	minHeapSize = project.hasProperty("test.jvm.heap.min-size")
		? project.getProperty("test.jvm.heap.min-size")
		: "512m"

}

integrationTest {

	maxHeapSize = project.hasProperty("test.jvm.heap.max-size")
		? project.getProperty("test.jvm.heap.max-size")
		: "2g"

	minHeapSize = project.hasProperty("test.jvm.heap.min-size")
		? project.getProperty("test.jvm.heap.min-size")
		: "512m"

	def gradleProjectPropertyLogbackLogLevel = project.hasProperty("logback.log.level")
		? project.getProperty("logback.log.level")
		: "ERROR";

	systemProperty "logback.log.level", System.getProperty("logback.log.level", gradleProjectPropertyLogbackLogLevel)

}
